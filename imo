#include <DHT.h>
#include <ArduinoJson.h>

// Pin Definitions
#define DHT_PIN 3          // DHT sensor pin -> D3
#define RELAY_PIN A2       // Fan relay pin -> A2
#define RELAY_PIN2 A3      // Humidifier relay pin -> A3
#define SOIL_ANALOG A0     // Soil moisture analog pin -> A0
#define SOIL_DIGITAL 2     // Soil moisture digital pin -> D2
#define BAUD_RATE 9600
#define READ_INTERVAL 2000
#define DHT_TYPE DHT11

DHT dht(DHT_PIN, DHT_TYPE);

unsigned long lastReadTime = 0;
bool relayState = false;
bool relay2State = false;

void setup() {
  Serial.begin(BAUD_RATE);
  delay(2000);

  Serial.println("Arduino is connected");

  pinMode(RELAY_PIN, OUTPUT);
  pinMode(RELAY_PIN2, OUTPUT);
  pinMode(SOIL_ANALOG, INPUT);
  pinMode(SOIL_DIGITAL, INPUT);
  pinMode(DHT_PIN, INPUT_PULLUP);

  dht.begin();
  delay(1000);

  digitalWrite(RELAY_PIN, HIGH);
  digitalWrite(RELAY_PIN2, HIGH);
}

void loop() {
  static unsigned long lastReadTime = 0;

  if (Serial.available() > 0) {
    String command = Serial.readStringUntil('\n');
    command.trim();

    if (command == "TEST") {
      Serial.println("Arduino is connected");
    }
    else if (command == "ON") {
      digitalWrite(RELAY_PIN, LOW);
      Serial.println("Relay 1 ON");
    }
    else if (command == "OFF") {
      digitalWrite(RELAY_PIN, HIGH);
      Serial.println("Relay 1 OFF");
    }
    else if (command == "ON2") {
      digitalWrite(RELAY_PIN2, LOW);
      Serial.println("Relay 2 ON");
    }
    else if (command == "OFF2") {
      digitalWrite(RELAY_PIN2, HIGH);
      Serial.println("Relay 2 OFF");
    }
  }

  if (millis() - lastReadTime >= READ_INTERVAL) {
    lastReadTime = millis();
    sendSensorData();
  }
}

void sendSensorData() {
  float temperature = dht.readTemperature();
  float humidity = dht.readHumidity();
  int soilMoistureRaw = analogRead(SOIL_ANALOG);
  int soilMoistureDigital = digitalRead(SOIL_DIGITAL);

  StaticJsonDocument<200> doc;
  doc["temperature"] = temperature;
  doc["humidity"] = humidity;
  doc["soilMoistureRaw"] = soilMoistureRaw;
  doc["soilMoistureDigital"] = soilMoistureDigital;

  serializeJson(doc, Serial);
  Serial.println();
}
